plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.semivanilla'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = "https://repo.mattstudios.me/artifactory/public/" }
    maven { url = "https://repo.dmulloy2.net/repository/public/" }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19-R0.1-SNAPSHOT'
    compileOnly files('./run/plugins/CookieCore.jar') // CookieCore, has h2 and serialization

    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.3'

    implementation 'dev.triumphteam:triumph-gui:3.1.2'

    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.8.0";
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
task copyPlugin(type: Copy) {
    from 'build/libs/NetherChests.jar'
    into 'run/plugins'
}
copyPlugin.dependsOn(shadowJar)
task runDev(type: JavaExec) {
    standardInput = System.in

    classpath = files('run/paper.jar')
    //run the serverJar, main from MANIFEST.MF
    //args('nogui')
    workingDir(file('run'))
}
runDev.dependsOn(project.tasks.copyPlugin)
shadowJar {
    archiveBaseName.set('NetherChests')
    archiveClassifier.set('')
    archiveVersion.set('')

    dependencies {
        include dependency('dev.triumphteam:triumph-gui:3.1.2')
    }

    relocate 'dev.triumphteam', 'com.semivanilla.netherchests.relocate.triumphteam'

}
